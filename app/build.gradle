/*
 * AI Interview Platform - Backend Application
 * Spring Boot 4.0 + Java 21 + Spring AI 2.0 + PostgreSQL + pgvector + RustFS + Redis
 */

plugins {
    id 'java'
    alias(libs.plugins.spring.boot)
    alias(libs.plugins.spring.dependency.management)
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.ai:spring-ai-bom:${libs.versions.spring.ai.get()}"
    }
}

group = 'com.interview'
version = '0.0.1-SNAPSHOT'

repositories {
    mavenCentral()
    maven { url 'https://repo.spring.io/milestone' }
}

dependencies {
    // Spring Boot Starters (Boot 4.0 modular design)
    implementation 'org.springframework.boot:spring-boot-starter-webmvc'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    // PostgreSQL
    runtimeOnly 'org.postgresql:postgresql'

    // Spring AI 2.0 - OpenAI兼容模式 (阿里云DashScope)
    implementation "org.springframework.ai:spring-ai-starter-model-openai:${libs.versions.spring.ai.get()}"
    // Spring AI 2.0 - PostgreSQL Vector Store (pgvector)
    implementation "org.springframework.ai:spring-ai-starter-vector-store-pgvector:${libs.versions.spring.ai.get()}"

    // DashScope Native SDK (for Image Generation) - 排除 slf4j-simple
    implementation('com.alibaba:dashscope-sdk-java:2.18.1') {
        exclude group: 'org.slf4j', module: 'slf4j-simple'
    }

    // Actuator
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    // Apache Tika for document parsing (PDF, DOCX, TXT)
    implementation libs.tika.core
    implementation libs.tika.parsers

    // AWS S3 SDK for RustFS storage - 排除 slf4j-simple
    implementation("software.amazon.awssdk:s3:${libs.versions.aws.sdk.get()}") {
        exclude group: 'org.slf4j', module: 'slf4j-simple'
    }

    // Redisson 4.0 - Redis客户端 (Boot 4.0 compatible) - 排除 slf4j-simple
    implementation("org.redisson:redisson-spring-boot-starter:${libs.versions.redisson.get()}") {
        exclude group: 'org.slf4j', module: 'slf4j-simple'
    }

    // Sa-Token 认证授权框架（核心包）
    implementation "cn.dev33:sa-token-spring-boot3-starter:${libs.versions.saToken.get()}"

    // BCrypt 密码加密（Spring Security Crypto）
    implementation 'org.springframework.security:spring-security-crypto:6.4.2'

    // PDF导出 - iText 8
    implementation "com.itextpdf:itext-core:${libs.versions.itext.get()}"
    implementation "com.itextpdf:font-asian:${libs.versions.itext.get()}"

    // Lombok
    compileOnly libs.lombok
    annotationProcessor libs.lombok

    // MapStruct - 对象映射 (必须在Lombok之后配置)
    implementation "org.mapstruct:mapstruct:${libs.versions.mapstruct.get()}"
    annotationProcessor "org.mapstruct:mapstruct-processor:${libs.versions.mapstruct.get()}"
    annotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0'

    // Testing
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation libs.junit.jupiter
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

// 全局强制排除 slf4j-simple（双保险）
configurations.all {
    resolutionStrategy {
        // 统一 SLF4J API 版本（可选，但推荐）
        force 'org.slf4j:slf4j-api:2.0.17'
        // 全局排除 slf4j-simple（防止其他隐蔽依赖引入）
        exclude group: 'org.slf4j', module: 'slf4j-simple'
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

tasks.named('test') {
    useJUnitPlatform()
}